;※※※※※※※※※※※※※※※※※※※※※※※※※※※※
;eraTheWorld 4.954 画蛇又添足 火焰猫燐别人版口上
;※※※※※※※※※※※※※※※※※※※※※※※※※※※※

;==================================================
;统一SUCCESS函数
;==================================================
@M_KOJO_AME_SUCCESS_K36_GENERAL
#LOCALSIZE 1
#LOCALSSIZE 1
;--------------------------------------------------
;某些指令有陷落阶段的限制
;--------------------------------------------------
SELECTCASE SELECTCOM
    CASE 304, 332, 333, 334, 335, 411, 412, 413, 445, 446, 448, 449, 480, 626
        SIF AME36_FALL_STATE() < 1
            TFLAG:192 = -2
    CASE 302, 305, 307, 309, 311, 336, 403, 431, 432, 436, 440, 442, 447, 603, 605, 610, 614, 617, 621
        SIF AME36_FALL_STATE() < 2
            TFLAG:192 = -2
    CASE 306, 308, 310, 312, 330, 354, 417, 615
        SIF AME36_FALL_STATE() < 3
            TFLAG:192 = -2
    CASE 350
        SIF AME36_FALL_STATE() < 4
            TFLAG:192 = -2
    CASE 357
        TFLAG:192 = -2
    ;--------------------------------------------------
    ;几个不花时间的指令中限制情绪的上升
    ;--------------------------------------------------
    CASE 271, 298, 299
        CFLAG:AME36_SUPPRESSION_TRIGGER = 1
ENDSELECT
IF TFLAG:192 == -2
    ;--------------------------------------------------
    ;在不影响指令执行的情况下，用睡睡魔法阻止阿燐参与
    ;--------------------------------------------------
    IF GROUPMATCH(SELECTCOM, 403, 411, 412, 413, 436, 445, 446, 448, 449, 621)
        TFLAG:192 = 0
        IF CFLAG:ASLEEP_AMER == 0
            CFLAG:AME36_SLEEPY_MAGIC = CFLAG:ACTION_AMER + 100
            CFLAG:ASLEEP_AMER = 1
            CFLAG:ACTION_AMER = 5
        ENDIF
    ;--------------------------------------------------
    ;否则，直接阻止指令的执行
    ;--------------------------------------------------
    ELSE
        SELECTCOM = 298
    ENDIF
    CFLAG:AME36_SUPPRESSION_TRIGGER = 1
ENDIF

;==================================================
;刻印取得的MESSAGECHECK
;因为调用比较靠后拿来当作EXTRASOURCE的补充了
;==================================================
@M_KOJO_AME_MESSAGE_MARKCNG_MESSAGECHECK_K36
#LOCALSIZE 1
#LOCALSSIZE 1
;--------------------------------------------------
;禁用原版的好感和信赖上升，并提前进行好感和信赖上升
;--------------------------------------------------
TFLAG:97 = 9999
TFLAG:13 = 1
TFLAG:98 = 0
CFLAG:FAVOR_AMER += CFLAG:AME36_COM_FAVOR
CFLAG:TRUST_AMER += CFLAG:AME36_COM_TRUST
RETURN 0

;==================================================
;自定义陷落条件检测
;==================================================
@M_KOJO_AME_CHECK_K36_FALL_STATE_OVERRIDE(ARGS, ARG, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN 1

;==================================================
;自定义陷落条件判定与显示
;==================================================
@M_KOJO_AME_CHECK_K36_FALL_STATE(ARGS, ARG, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARGS
    ;--------------------------------------------------
    ;思慕
    ;--------------------------------------------------
    CASE "YEARNING"
        RESULTS:1 = 思慕未达成的条件
        RESULT:1 = 0
        RESULTS:2 = 思慕已达成的条件
        RESULT:2 = 1
        RETURN 2
    ;--------------------------------------------------
    ;恋慕
    ;--------------------------------------------------
    CASE "LOVE"
        RESULTS:1 = 恋慕未达成的条件
        RESULT:1 = 0
        RESULTS:2 = 恋慕已达成的条件
        RESULT:2 = 1
        RETURN 2
    ;--------------------------------------------------
    ;炮友
    ;--------------------------------------------------
    CASE "FUCK BUDDY"
        RESULTS:1 = 取得不可
        RESULT:1 = 0
        RETURN 1
    ;--------------------------------------------------
    ;爱欲
    ;--------------------------------------------------
    CASE "LUST"
        RESULTS:1 = 取得不可
        RESULT:1 = 0
        RETURN 1
ENDSELECT
RETURN 0

;================================
;自定义商店按钮
;Y 是按钮的编号，范围是0-9
;使用的时候请删除SKIPSTART~SKIPEND。
;================================
[SKIPSTART]
;判断按钮是否可以显示，如果返回1，则显示
@KOJO_AME_CUSTOM_BUTTON_CONDITION_K36_Y
;custom_button_name:XX:Y = ボタンY
RETURN 1

;按钮被按下后，触发以下函数
@KOJO_AME_CUSTOM_BUTTON_K36_Y

RETURN 1
[SKIPEND]

;==================================================
;自定义COUNTER触发概率
;ARG为COUNTER编号
;==================================================
@M_KOJO_AME_CHECK_K36_CCOM_ORDER(ARGS, ARG, ARG:2)
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN -1

;==================================================
;自定义移动倾向以及特殊食谱
;==================================================
@M_KOJO_AME_CHECK_K36_CHARADATA(ARGS, ARG, ARG:2)
#LOCALSIZE 1
#LOCALSSIZE 1
;--------------------------------------------------
;覆盖原版的@CHARAMOVE_DATA_36
;--------------------------------------------------
IF ARGS == "MOVE"
    SELECTCASE ARG
        ;--------------------------------------------------
        ;控制外出时一同遭遇
        ;跟在阿空或觉大人身边各1/4概率
        ;--------------------------------------------------
        CASE 1
       		IF BETWEENTIME(660, 1080) || (AME36_WEEKDAY_MATCH(0, 6) && BETWEENTIME(420, 1080))
                IF !RAND:4
                    RETURN 37
                ELSEIF !RAND:3
                    RETURN 49
                ENDIF
            ENDIF
            RETURN 36
       	;--------------------------------------------------
        ;生活时间补正的修改（暂时用不到）
        ;--------------------------------------------------
       	;CASE 3
       	;--------------------------------------------------
        ;移动倾向
        ;大于0表示偏好，小于0表示少去，-100表示不去
        ;--------------------------------------------------
       	CASE 4
       		SELECTCASE ARG:2
                ;--------------------------------------------------
                ;如果阿空在房间就一起玩
                ;--------------------------------------------------
                CASE 911
                    SIF CFLAG:37:LOCATION_AMER == 911
                        RETURN 10
       			;--------------------------------------------------
                ;偏爱地点：书斋、蔷薇园、酒场、温泉
                ;真的有不喜欢看书赏花喝酒泡温泉的猫嘛
                ;--------------------------------------------------
       			CASE 920, 925, 933, 934
       				RETURN 5
       			;--------------------------------------------------
                ;偏好地点：厨房、露台、风吕、川岸、桥
                ;给觉大人做完饭看看风景
                ;--------------------------------------------------
       			CASE 909, 915, 921, 945, 946
       				RETURN 3
                ;--------------------------------------------------
                ;少去地点：大会堂、觉大人的房间、地狱的人工太阳、旧血池地狱
                ;主要是确实没什么理由去的地方
                ;--------------------------------------------------
                CASE 904, 919, 929, 930
                    RETURN -3
                ;--------------------------------------------------
                ;更少去地点：恋大人的房间、鬼的聚集场所、血枪的房间
                ;--------------------------------------------------
                CASE 918, 938, 947
                    RETURN -5
                ;--------------------------------------------------
                ;不去地点：两个MASTER可选的初期房间，情人旅馆，勇仪、帕露、山女、小桶的房间，某个空房间，托卡马克装置核心（反正也进不去）
                ;--------------------------------------------------
       			CASE 916, 935, 936, 937, 940, 941, 942, 943, 949
       				RETURN -100
       		ENDSELECT
            ;--------------------------------------------------
            ;因为原版代码的bug，如果 RETURN 0 就会被原版覆盖，所以只能全部都 RETURN 1 了
            ;--------------------------------------------------
            RETURN 1
       	;--------------------------------------------------
        ;在主地图外的出现场所
        ;--------------------------------------------------
       	CASE 7
       		SELECTCASE ARG:2
                ;--------------------------------------------------
                ;博丽神社：樱花道的鸟居、神社境内、神社本堂、神社走廊
                ;--------------------------------------------------
       			CASE 10, 20, 30, 40
       				RETURN 1
                ;--------------------------------------------------
                ;命莲寺：墓地
                ;--------------------------------------------------
       			CASE 150
       				RETURN 1
                ;--------------------------------------------------
                ;人里：商家町
                ;--------------------------------------------------
                CASE 220
                    RETURN 1
                ;--------------------------------------------------
                ;魔法森林：无缘塚
                ;--------------------------------------------------
                CASE 570
                    RETURN 1
                ;--------------------------------------------------
                ;三途之川冥界：迷途之家
                ;--------------------------------------------------
                CASE 630
                    RETURN 1
                ;--------------------------------------------------
                ;妖怪之山山麓：间歇泉地下中枢
                ;--------------------------------------------------
       			CASE 760
       				RETURN 1
                ;--------------------------------------------------
                ;地底：幻想风穴、地狱的深道、旧地狱街道、旧地狱温泉、地灵殿、灼热地狱迹
                ;--------------------------------------------------
       			CASE 910, 920, 930, 940, 960, 970
       				RETURN 1
       		ENDSELECT
        ;--------------------------------------------------
        ;引力点的设定
        ;用了这个就不需要用GRAVITY函数了
        ;--------------------------------------------------
       	CASE 8
            ;--------------------------------------------------
            ;当觉大人或阿空处于危险状态时，立马赶过去
            ;WIP：根据警惕值计算行动概率
            ;--------------------------------------------------
       		IF AME36_DANGEROUS(49)
                TCVAR:GRAVITY_AMER = CFLAG:49:LOCATION_AMER
                RETURN 1
            ENDIF
            IF AME36_DANGEROUS(37)
                TCVAR:GRAVITY_AMER = CFLAG:37:LOCATION_AMER
                RETURN 1
            ENDIF
            ;--------------------------------------------------
            ;平时1/10概率跟着觉大人
            ;--------------------------------------------------
            SIF CFLAG:AME36_RAND_TODAY % 10 == 0 && AME36_FOLLOWABLE(49)
                TCVAR:GRAVITY_AMER = CFLAG:49:LOCATION_AMER
            RETURN 1
    ENDSELECT
;--------------------------------------------------
;话说这样做真的好恰嘛
;--------------------------------------------------
ELSEIF ARGS == "SPECIAL_MENU"
	DISH_NAME = 琥珀渔夫派
	RESULTS = 主食
	CALL 材料設定("魚", "好吃的魚")
	DISH_TASTE = 魚料理/牛奶/卵/派/洋食/地底風/怪诞/简易/質素/家庭的/软绵绵/软绵绵/甜/咸/
	DISH_COMMENT = 用红薯替代土豆泥、焦糖与蜂蜜凝成琥珀外壳。甜咸交融中、是西洋与地底怪诞风格的碰撞。
	RETURN 1
ENDIF
RETURN 0
