;※※※※※※※※※※※※※※※※※※※※※※※※※※※※
;eraTheWorld 4.954 画蛇又添足 火焰猫燐别人版口上
;※※※※※※※※※※※※※※※※※※※※※※※※※※※※

;==================================================
;COM271 - 猫车服务
;==================================================
;指令名
;--------------------------------------------------
@KOJO_AME_COM_NAME_K36_1
#LOCALSIZE 1
#LOCALSSIZE 1
RESULTS = 猫车服务
RETURN 0

;--------------------------------------------------
;指令的执行条件（需要相识，其他与会话基本相同）
;--------------------------------------------------
@KOJO_AME_COM_ABLE_K36_1
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !CFLAG:AME36_ACQUAINTANCE
    RETURN 0
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(300)
	RETURN RESULT
SIF CFLAG:ASLEEP_AMER && !CFLAG:SNUGGLE_AMER
	RETURN 0
SIF FLAG:TIMESTOP_AMER
	RETURN 0
RETURN 1

;--------------------------------------------------
;指令的成功判定
;--------------------------------------------------
@KOJO_AME_COM_K36_1
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN 1

;--------------------------------------------------
;指令的主内容
;--------------------------------------------------
@M_KOJO_AME_MESSAGE_COM_K36_271
#LOCALSIZE 3
#LOCALSSIZE 1
#DIM DYNAMIC HAS_WORK
#DIM DYNAMIC DESTINATION
RESETCOLOR
LOCAL = LINECOUNT
LOCALS = 「有什么事情嘛、%CALLNAME:MASTER%/
;--------------------------------------------------
;今天有工作，但还没开始
;--------------------------------------------------
IF TCVAR:JOB_START_AMER > 0 && TIME < JOB_START_AMER
    LOCALS += "今天的工作还没开始呢"
    HAS_WORK = 1
;--------------------------------------------------
;正在工作
;--------------------------------------------------
ELSEIF CFLAG:ACTION_AMER == 5
    LOCALS += "咱正忙着工作呢"
    HAS_WORK = 1
;--------------------------------------------------
;今天的工作已经结束了
;--------------------------------------------------
ELSEIF TCVAR:JOB_END_AMER > 0 && TIME >= TCVAR:JOB_END_AMER
    LOCALS += "今天的工作已经做完啦、要一起玩些什么嘛"
;--------------------------------------------------
;今天要求阿燐请假了
;--------------------------------------------------
ELSEIF TCVAR:JOB_START_AMER == -2
    LOCALS += @"今天为%CALLNAME:MASTER%请假了、接下来去哪里呢"
;--------------------------------------------------
;今天没有工作
;--------------------------------------------------
ELSE
    LOCALS += "今天咱放假、要好好休息还是出去玩呢"
ENDIF
LOCALS += "」" 
CALL SPTALK, 36, "通常", 0, LOCALS, 1
PRINTFORML 要请求阿燐做什么呢……
DRAWLINE
IF AME36_FALL_STATE() == 1
    CALL AME36_CONDITIONAL_PRINT("[0]一起去别的地方吧", CFLAG:ACTION_AMER != 5 && MAIN_MAP == 9)
ELSE
    CALL AME36_CONDITIONAL_PRINT("[0]TBD", 0)
ENDIF
CALL AME36_CONDITIONAL_PRINT("[1]今天的工作可以请假嘛", HAS_WORK && AME36_FALL_STATE() > 1)
PRINTFORML [2]还是算了
DRAWLINE
INPUTS
;--------------------------------------------------
;因为思慕之前不能同行，该选项作为同行的替代品，但只有住在地底才能用
;--------------------------------------------------
IF RESULTS == "0"
    IF AME36_FALL_STATE() == 1 && CFLAG:ACTION_AMER != 5 && MAIN_MAP == 9
        LOCAL:1 = LINECOUNT
        $ACCOMPANY_GENERAL
        DRAWLINE
        PRINTFORML [900]来我的房间吧
        PRINTFORML [909]去做料理吧
        PRINTFORML [912]去阿燐的房间吧
        PRINTFORML [920]去读书吧
        PRINTFORML [926]去活动下身体吧
        PRINTFORML [931]去逛街吧
        PRINTFORML [948]去捕猎吧
        PRINTFORML [998]去捕虫吧
        PRINTFORML [999]去采集素材吧
        PRINTFORML [1000]还是算了
        PRINTL
        INPUTS
        SELECTCASE RESULTS
            CASE "900"
                DESTINATION = CFLAG:MASTER:RESIDENCE_AMER
                TFLAG:193 = 900
            CASE "909", "912", "920", "926", "931", "948"
                DESTINATION = TOINT(RESULTS)
                TFLAG:193 = DESTINATION
            CASE "998"
                DRAWLINE
                PRINTFORML 去哪里呢……
                PRINTFORML [901]地灵殿前
                PRINTFORML [923]后院
                PRINTFORML [925]蔷薇园
                PRINTFORML [926]中庭
                PRINTFORML [939]小胡同
                PRINTFORML [945]川岸
                PRINTFORML [948]纵穴底部
                PRINTFORML [1000]还是算了
                PRINTL
                INPUTS
                SELECTCASE RESULTS
                    CASE "901", "923", "925", "926", "939", "945", "948"
                        DESTINATION = TOINT(RESULTS)
                        TFLAG:193 = 998
                    CASEELSE
                        CLEARLINE LINECOUNT - LOCAL:1
                        GOTO ACCOMPANY_GENERAL
                ENDSELECT
            CASE "999"
                DRAWLINE
                PRINTFORML 去哪里呢……
                PRINTFORML [928]灼热地狱迹
                PRINTFORML [945]川岸
                PRINTFORML [948]纵穴底部
                PRINTFORML [1000]还是算了
                PRINTL
                INPUTS
                SELECTCASE RESULTS
                    CASE "928", "945", "948"
                        DESTINATION = TOINT(RESULTS)
                        TFLAG:193 = 999
                    CASEELSE
                        CLEARLINE LINECOUNT - LOCAL:1
                        GOTO ACCOMPANY_GENERAL
                ENDSELECT
        ENDSELECT
        IF DESTINATION
            IF DESTINATION == CFLAG:LOCATION_AMER
                CALL M_KOJO_AME_COLOR_K36
                PRINTFORMW 「欸、但咱们不是已经在这里了嘛」
                RESETCOLOR
            ELSE
                IF GETBIT(CAN_MOVE(CFLAG:LOCATION_AMER, DESTINATION), 0)
                    TIME += 1
                ELSE
                    TIME += 2
                ENDIF
                CFLAG:LOCATION_AMER = DESTINATION
                CFLAG:MASTER:LOCATION_AMER = DESTINATION
                RETURN 1
            ENDIF
        ENDIF
    ELSEIF AME36_FALL_STATE() > 1 && 0
        ;TBD
    ENDIF
;--------------------------------------------------
;思慕每月可以请假1天，恋慕2天，恋人3天
;--------------------------------------------------
ELSEIF RESULTS == "1" && HAS_WORK && AME36_FALL_STATE() > 1
    SELECTCASE AME36_FALL_STATE()
        CASE 2
            IF CFLAG:AME36_LEAVE_DAYS == 0
                TFLAG:193 = 11
                GOTO DAY_OFF
            ELSE
                TFLAG:193 = 12
            ENDIF
        CASE 3
            IF CFLAG:AME36_LEAVE_DAYS == 0
                TFLAG:193 = 13
                GOTO DAY_OFF
            ELSEIF CFLAG:AME36_LEAVE_DAYS == 1
                TFLAG:193 = 14
                TIME += 5
                GOTO DAY_OFF
            ELSE
                TFLAG:193 = 15
            ENDIF
        CASE 4
            IF CFLAG:AME36_LEAVE_DAYS == 0
                TFLAG:193 = 16
                GOTO DAY_OFF
            ELSEIF CFLAG:AME36_LEAVE_DAYS == 1
                TFLAG:193 = 17
                GOTO DAY_OFF
            ELSEIF CFLAG:AME36_LEAVE_DAYS == 2
                TFLAG:193 = 18
                TIME += 5
                GOTO DAY_OFF
            ELSE
                TFLAG:193 = 19
            ENDIF
    ENDSELECT
    RETURN 1
ELSEIF RESULTS == "2"
    TFLAG:193 = 0
    RETURN 1
ENDIF
CLEARLINE LINECOUNT - LOCAL
RESTART
;--------------------------------------------------
;请假的处理
;--------------------------------------------------
$DAY_OFF
CFLAG:AME36_LEAVE_DAYS += 1
BASE:WORK_REMAINING_AMER = 0
TCVAR:JOB_START_AMER = -2
TCVAR:JOB_END_AMER = -2
TIME += 5
RETURN 1

;==================================================
;COM298 - 指令失败时的占位函数
;==================================================
;指令名
;--------------------------------------------------
@KOJO_AME_COM_NAME_K36_28
#LOCALSIZE 1
#LOCALSSIZE 1
RESULTS = 不可以哦
RETURN 0

;--------------------------------------------------
;指令的执行条件（无法主动执行）
;--------------------------------------------------
@KOJO_AME_COM_ABLE_K36_28
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN 0

;--------------------------------------------------
;指令的成功判定
;--------------------------------------------------
@KOJO_AME_COM_K36_28
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN 1

;--------------------------------------------------
;指令的主内容
;--------------------------------------------------
@M_KOJO_AME_MESSAGE_COM_K36_298
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN -1

;==================================================
;COM299 - 回忆值的显示
;==================================================
;指令名
;--------------------------------------------------
@KOJO_AME_COM_NAME_K36_29
#LOCALSIZE 1
#LOCALSSIZE 1
RESULTS = 回忆值
RETURN 0

;--------------------------------------------------
;指令的执行条件（与会话基本相同）
;--------------------------------------------------
@KOJO_AME_COM_ABLE_K36_29
#LOCALSIZE 1
#LOCALSSIZE 1
;SIF AME36_FALL_STATE() < 3
;    RETURN 0
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(300)
	RETURN RESULT
SIF CFLAG:ASLEEP_AMER && !CFLAG:SNUGGLE_AMER
	RETURN 0
SIF FLAG:TIMESTOP_AMER
	RETURN 0
RETURN 1

;--------------------------------------------------
;指令的成功判定
;--------------------------------------------------
@KOJO_AME_COM_K36_29
#LOCALSIZE 1
#LOCALSSIZE 1
RETURN 1

;--------------------------------------------------
;指令的主内容
;--------------------------------------------------
@M_KOJO_AME_MESSAGE_COM_K36_299
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM CURRENT_PAGE = 1
#DIM MAX_PAGE = 3
#DIMS CURRENT_MODE = "444"
#DIM START_LINECOUNT
{
#DIMS ALCOHOL_NAMES = "浊酒", "清酒", "鬼杀酒", "红酒", "神酒", "甜酒", "蒸馏酒", "药酒", "一夜奇稻田", 
                      "烧酒", "蜂蜜酒", "果酒", "莓子酒", "白朗姆酒", "金朗姆酒", "黑朗姆酒", "强化红酒",
                      "贵腐红酒", "陈酿红酒", "优质陈酿红酒", "白兰地ＶＳ", "白兰地ＶＳＯＰ", "白兰地ＸＯ", 
                      "灰持酒", "果渣白兰地", "日本浊酒", "吟酿酒", "熟成酒", "古酒", "酒粕烧酎‌", 
                      "贵酿日本浊酒", "贵酿酒", "贵酿熟成酒", "贵酿古酒", "三增酒"
}
#DIMS TDISH_NAME
#DIMS TDISH_TASTE
#DIMS TDISH_TYPE
#DIMS TDISH_COMMENT
#DIM TDISH_ING, 11
#DIM TDISH_TIME
#DIM TDISH_MAXLIMIT
RESETCOLOR

PRINTFORMW 与阿燐忆起相识以来的种种过往……
CLEARLINE 1
START_LINECOUNT = LINECOUNT
$RECOLLECTION_LOOP
;--------------------------------------------------
;这里是表头
;--------------------------------------------------
PRINTFORML 总回忆值：{AME36_RECOLLECTION()}
DRAWLINE
;--------------------------------------------------
;这里是表格的主内容
;--------------------------------------------------
IF CURRENT_MODE == "444"
    IF CURRENT_PAGE == 1
        PRINTPLAINFORM [300]会话　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_300, 35, 100, 340, 890, 2000, 1000), 4000, 1)
        PRINTPLAINFORM [301]泡茶　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_301, 21, 60, 210, 560, 1200, 720), 2000, 1)
        PRINTPLAINFORM [302]身体接触　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_302, 0, 0, 180, 580, 1200, 480), 3000, 1)
        PRINTPLAINFORM [303]道歉　　　　　　
        SELECTCASE CFLAG:AME36_RECOLLECTION_303
            CASE -1
                CALL COLORMESSAGE("惹阿燐生气却不道歉的家伙(-200)", C_RED, 1)
            CASE 0
                CALL COLORMESSAGE("没有惹阿燐生气过哦", C_AQUA, 1)
            CASE 1
                CALL COLORMESSAGE("有好好道歉过、所以没有惩罚", C_GREEN, 1)
        ENDSELECT
        PRINTPLAINFORM [304]帮忙干活　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_304, 0, 26, 116, 316, 700, 245), 2000, 1)
        PRINTPLAINFORM [305]膝枕／索求膝枕　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_305, 0, 0, 90, 290, 600, 300), 1200, 1)
        PRINTPLAINFORM [306]抚摸小腹　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_306, 0, 0, 0, 100, 300, 150), 600, 1)
        PRINTPLAINFORM [307]捏脸蛋　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_307, 0, 0, 90, 290, 600, 450), 800, 1)
        PRINTPLAINFORM [308]戳脸颊　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_308, 0, 0, 0, 100, 300, 150), 600, 1)
        PRINTPLAINFORM [309]摸头　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_309, 0, 0, 90, 290, 600, 300), 1200, 1)
        PRINTPLAINFORM [310]摸屁股　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_310, 0, 0, 0, 200, 600, 600), 600, 1)
        PRINTPLAINFORM [311]拥抱　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_311, 0, 0, 90, 290, 600, 300), 1200, 1)
        PRINTPLAINFORM [330]掀起裙子　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_330, 0, 0, 0, 100, 300, 225), 400, 1)
        PRINTPLAINFORM [332]劝酒　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_332, 0, 13, 58, 158, 360, 216), 600, 1)
        PRINTPLAINFORM [333]听牢骚　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_333, 0, 1, 2, 3, 5, -8), 200, 1)
        PRINTPLAINFORM [334]碰杯／照料　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_334, 0, 8, 38, 104, 240, 72), 800, 1)
        PRINTPLAINFORM [354]陪睡　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_354, 0, 0, 0, 100, 300, 150), 600, 1)
        PRINTPLAINFORM [403]休憩　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_403, 0, 0, 10, 20, 50, 5), 500, 1)
        PRINTPLAINFORM [410]扫除　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_410, 14, 40, 130, 330, 700, 490), 1000, 1)
        PRINTPLAINFORM [411]战斗训练　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_411, 0, 52, 232, 632, 1500, 750), 3000, 1)
    ELSEIF CURRENT_PAGE == 2
        PRINTPLAINFORM [412]学习　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_412, 0, 52, 232, 632, 1500, 750), 3000, 1)
        PRINTPLAINFORM [413]制作料理　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_413, 0, 52, 232, 632, 1500, 1125), 2000, 1)
        PRINTPLAINFORM [414]吃饭／招待吃饭　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_414, 14, 40, 130, 330, 720, 432), 1200, 1)
        PRINTPLAINFORM [416]演奏　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_416, 4, 13, 43, 109, 240, 72), 800, 1)
        PRINTPLAINFORM [417]午睡　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_417, 0, 0, 0, 4, 10, -4), 400, 1)
        PRINTPLAINFORM [431]在浴室里放松　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_431, 0, 0, 90, 290, 600, 360), 1000, 1)
        PRINTPLAINFORM [432]钱汤　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_432, 0, 0, 12, 40, 100, 25), 400, 1)
        PRINTPLAINFORM [436]读书　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_436, 0, 0, 90, 290, 600, 240), 1500, 1)
        PRINTPLAINFORM [440]等待　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_440, 0, 0, 2, 5, 10, -2), 200, 1)
        PRINTPLAINFORM [442]伐采　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_442, 0, 0, 12, 40, 100, 20), 500, 1)
        PRINTPLAINFORM [445]采集　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_445, 0, 26, 216, 316, 700, 245), 2000, 1)
        PRINTPLAINFORM [446]进行调合　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_446, 0, 13, 58, 158, 400, 80), 2000, 1)
        PRINTPLAINFORM [447]路边摆摊　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_447, 0, 0, 2, 5, 10, -8), 800, 1)
        PRINTPLAINFORM [448]设下陷阱　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_448, 0, 1, 2, 5, 10, -4), 400, 1)
        PRINTPLAINFORM [449]捕虫　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_449, 0, 2, 5, 10, 20, 1), 400, 1)
        PRINTPLAINFORM [480]弹幕胜负　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_480, 0, 1, 2, 5, 10, -6), 600, 1)
        PRINTPLAINFORM [483]斗虫　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_483, 1, 1, 2, 3, 5, -8), 200, 1)
        PRINTPLAINFORM [484]打西瓜　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_484, 1, 1, 2, 3, 5, -8), 200, 1)
        PRINTPLAINFORM [603]牵手／拉手　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_603, 0, 0, 90, 290, 600, 450), 800, 1)
        PRINTPLAINFORM [605]绕道　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_605, 0, 0, 2, 5, 10, -4), 400, 1)
    ELSEIF CURRENT_PAGE == 3
        PRINTPLAINFORM [610]请吃饭　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_610, 0, 0, 2, 5, 10, -4), 400, 1)
        PRINTPLAINFORM [614]采购食材　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_614, 0, 0, 1, 2, 5, -8), 200, 1)
        PRINTPLAINFORM [615]吃便当　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_615, 0, 0, 0, 66, 200, 80), 500, 1)
        PRINTPLAINFORM [617]邀请去甜品店　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_617, 0, 0, 1, 2, 5, -16), 400, 1)
        PRINTPLAINFORM [621]钓鱼　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_621, 0, 0, 180, 580, 1300, 845), 2000, 1)
        PRINTPLAINFORM [626]送礼　　　　　　
        CALL AME36_COMPLETION_PRINT(AME36_COM_RECOLLECTION(CFLAG:AME36_RECOLLECTION_626, 0, 5, 10, 20, 50, 1), 2500, 1)
    ENDIF
ELSEIF CURRENT_MODE == "555"
    
ELSEIF CURRENT_MODE == "666"
    PRINTFORM 与阿燐一起度过的时光　
    CALL AME36_COMPLETION_PRINT(AME36_PERSISTENCE_RECOLLECTION(), 10000, 1)
    PRINTBUTTON "喝过酒的种类　　　　　", "901"
    CALL AME36_COMPLETION_PRINT(AME36_ALCOHOL_RECOLLECTION(), 1000, 1)
    PRINTBUTTON "做过料理的种类　　　　", "902"
    CALL AME36_COMPLETION_PRINT(AME36_DISH_RECOLLECTION(), 2000, 1)
ENDIF
DRAWLINE
;--------------------------------------------------
;这里是表尾的选项部分
;--------------------------------------------------
IF CURRENT_PAGE == 1
    SETCOLOR C_L_GRAY
    PRINTPLAIN [111]　上一页
    RESETCOLOR
ELSE
    PRINTBUTTON "[111]　上一页", "111"
ENDIF
PRINT 　　　　
PRINTBUTTON "[222]　结束回忆", "222"
PRINT 　　　
IF CURRENT_PAGE == MAX_PAGE
    SETCOLOR C_L_GRAY
    PRINTPLAIN [333]　下一页
    RESETCOLOR
ELSE
    PRINTBUTTON "[333]　下一页", "333"
ENDIF
PRINTL
IF CURRENT_MODE == "444"
    SETCOLOR C_L_GRAY
    PRINTPLAIN [444]　ＣＯＭ相关
    RESETCOLOR
ELSE
    PRINTBUTTON "[444]　ＣＯＭ相关", "444"
ENDIF
PRINT 　　
IF CURRENT_MODE == "555"
    SETCOLOR C_L_GRAY
    PRINTPLAIN [555]　特殊事件
    RESETCOLOR
ELSE
    PRINTBUTTON "[555]　特殊事件", "555"
ENDIF
PRINT 　　　
IF CURRENT_MODE == "666"
    SETCOLOR C_L_GRAY
    PRINTPLAIN [666]　杂项
    RESETCOLOR
ELSE
    PRINTBUTTON "[666]　杂项", "666"
ENDIF
INPUTS
SIF RESULTS == "222"
    RETURN 1
SIF RESULTS == "111"
    CURRENT_PAGE = MAX(CURRENT_PAGE - 1, 1)
SIF RESULTS == "333"
    CURRENT_PAGE = MIN(CURRENT_PAGE + 1, MAX_PAGE)
IF GROUPMATCH(RESULTS, "444", "555", "666")
    CURRENT_MODE '= RESULTS
    CURRENT_PAGE = 1
    SELECTCASE RESULTS
        CASE "444"
            MAX_PAGE = 3
        CASE "555"
            MAX_PAGE = 1
        CASE "666"
            MAX_PAGE = 1
    ENDSELECT
ENDIF
;--------------------------------------------------
;酒和料理的完成度显示
;--------------------------------------------------
IF CURRENT_MODE == "666"
    IF RESULTS == "901"
        DRAWLINE
        PRINTFORML □都一起喝过什么酒呢□
        FOR LOCAL, 0, 34
            SIF GETBIT(CFLAG:AME36_ALCOHOL_TYPE, LOCAL)
                SETCOLOR C_GREEN
            STRLENFORM %ALCOHOL_NAMES:LOCAL%
            PRINTFORM %ALCOHOL_NAMES:LOCAL%%"　" * (8 - RESULT / 2)%
            RESETCOLOR
            SIF (LOCAL + 1) % 3 == 0
                PRINTL
        NEXT
        PRINTL
        PRINTW
    ELSEIF RESULTS == "902"
        VARSET LOCAL
        TDISH_NAME '= DISH_NAME
        TDISH_TASTE '= DISH_TASTE
        TDISH_TYPE '= DISH_TYPE
        TDISH_COMMENT '= DISH_COMMENT
        FOR LOCAL, 0, 11
	        TDISH_ING:LOCAL = DISH_ING:LOCAL
        NEXT
        TDISH_TIME = DISH_TIME
        TDISH_MAXLIMIT = DISH_MAXLIMIT

        DRAWLINE
        PRINTFORML □都一起做过什么料理呢□
        PRINTFORML ＜轻食＞
        FOR LOCAL, 101, 200
            SELECTCASE LOCAL
                CASE 101 TO 103, 111 TO 119, 121 TO 127, 131 TO 146, 151 TO 166, 171 TO 174, 181 TO 185, 191 TO 196
                    IF LOCAL:1 <= 63
                        SIF GETBIT(CFLAG:AME36_DISH_TYPE_1, LOCAL:1)
                            SETCOLOR C_GREEN
                    ELSE
                        SIF GETBIT(CFLAG:AME36_DISH_TYPE_2, LOCAL:1 - 64)
                            SETCOLOR C_GREEN
                    ENDIF
                    CALL DISHDATA(LOCAL)
                    STRLENFORM %DISH_NAME%
                    PRINTFORM %DISH_NAME%%"　" * (10 - RESULT / 2)%
                    RESETCOLOR
                    SIF (LOCAL:1 + 1) % 4 == 0
                        PRINTL
                    LOCAL:1 += 1
            ENDSELECT
        NEXT
        PRINTL
        PRINTL
        PRINTFORML ＜主食＞
        FOR LOCAL, 201, 300
            SELECTCASE LOCAL
                CASE 201 TO 221, 231 TO 238, 241 TO 251, 261 TO 273, 281 TO 287, 291 TO 295
                    IF LOCAL:1 <= 127
                        SIF GETBIT(CFLAG:AME36_DISH_TYPE_2, LOCAL:1 - 64)
                            SETCOLOR C_GREEN
                    ELSE
                        SIF GETBIT(CFLAG:AME36_DISH_TYPE_3, LOCAL:1 - 128)
                            SETCOLOR C_GREEN
                    ENDIF
                    CALL DISHDATA(LOCAL)
                    STRLENFORM %DISH_NAME%
                    PRINTFORM %DISH_NAME%%"　" * (10 - RESULT / 2)%
                    RESETCOLOR
                    SIF (LOCAL:1 - 1) % 4 == 0
                        PRINTL
                    LOCAL:1 += 1
            ENDSELECT
        NEXT
        PRINTL
        PRINTL
        PRINTFORML ＜甜点＞
        FOR LOCAL, 301, 350
            SELECTCASE LOCAL
                CASE 301 TO 330, 341 TO 343
                    SIF GETBIT(CFLAG:AME36_DISH_TYPE_3, LOCAL:1 - 128)
                        SETCOLOR C_GREEN
                    CALL DISHDATA(LOCAL)
                    STRLENFORM %DISH_NAME%
                    PRINTFORM %DISH_NAME%%"　" * (10 - RESULT / 2)%
                    RESETCOLOR
                    SIF (LOCAL:1 - 2) % 4 == 0
                        PRINTL
                    LOCAL:1 += 1
            ENDSELECT
        NEXT
        PRINTL
        PRINTL
        PRINTFORML ＜跳蛋？＞
        FOR LOCAL, 351, 360
            SELECTCASE LOCAL
                CASE 351 TO 355
                    SIF GETBIT(CFLAG:AME36_DISH_TYPE_3, LOCAL:1 - 128)
                        SETCOLOR C_GREEN
                    CALL DISHDATA(LOCAL)
                    STRLENFORM %DISH_NAME%
                    PRINTFORM %DISH_NAME%%"　" * (10 - RESULT / 2)%
                    RESETCOLOR
                    SIF (LOCAL:1 + 1) % 4 == 0
                        PRINTL
                    LOCAL:1 += 1
            ENDSELECT
        NEXT
        PRINTL
        PRINTL
        LOCAL:1 = 1
        PRINTFORML ＜固有料理＞
        IF CFLAG:AME36_DISH_TYPE_C1 + CFLAG:AME36_DISH_TYPE_C2 + CFLAG:AME36_DISH_TYPE_C3 == 0
            PRINTFORML 还什么都没有哦
        ELSE
            SETCOLOR C_GREEN
            FOR LOCAL, 401, 401 + CHARANUM
                IF LOCAL <= 464
                    SIF !GETBIT(CFLAG:AME36_DISH_TYPE_C1, LOCAL - 401)
                        CONTINUE
                ELSEIF LOCAL <= 528
                    SIF !GETBIT(CFLAG:AME36_DISH_TYPE_C1, LOCAL - 465)
                        CONTINUE
                ELSEIF LOCAL <= 592
                    SIF !GETBIT(CFLAG:AME36_DISH_TYPE_C1, LOCAL - 529)
                        CONTINUE
                ENDIF
                CALL DISHDATA(LOCAL)
                STRLENFORM %DISH_NAME%
                PRINTFORM %DISH_NAME%
                IF RESULT % 2 == 1
                    PRINTFORM %" "%
                    RESULT += 1
                ENDIF
                PRINTFORM %"　" * (10 - RESULT / 2)%
                SIF LOCAL:1 % 4 == 0
                    PRINTL
                LOCAL:1 += 1
            NEXT
            RESETCOLOR
            PRINTL
        ENDIF
        PRINTW

        DISH_NAME '= TDISH_NAME
        DISH_TASTE '= TDISH_TASTE
        DISH_TYPE '= TDISH_TYPE
        DISH_COMMENT '= TDISH_COMMENT
        FOR LOCAL, 0, 11
        	DISH_ING:LOCAL = TDISH_ING:LOCAL
        NEXT
        DISH_TIME = TDISH_TIME
        DISH_MAXLIMIT = TDISH_MAXLIMIT
    ENDIF
ENDIF
CLEARLINE LINECOUNT - START_LINECOUNT
GOTO RECOLLECTION_LOOP

CALL M_KOJO_AME_COLOR_K36
RETURN 1

;--------------------------------------------------
;为了简化代码的一个工具函数
;--------------------------------------------------
@AME36_COMPLETION_PRINT(PROGRESS, MAX_VALUE, IS_L)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM PROGRESS
#DIM MAX_VALUE
#DIM IS_L
IF PROGRESS == MAX_VALUE
    LOCALS = ({PROGRESS}/{MAX_VALUE})
    CALL COLORMESSAGE(LOCALS, C_GREEN, IS_L)
ELSE
    IF IS_L
        PRINTFORML ({PROGRESS}/{MAX_VALUE})
    ELSE
        PRINTFORM ({PROGRESS}/{MAX_VALUE})
    ENDIF
ENDIF
RETURN 1